#!/usr/bin/env cbqn
# summarize a CSV file generated by `benchm`
# Usage: parse.bqn generated.csv
env←{
  vars ⇐ ⍉>⊑∘⊐⟜'='⊸(↑⋈1⊸↓∘↓)¨{-¬(¬×1++`)𝕩=@+10}⊸⊔1⊑•SH<"env"
  Var ⇐ {⊐⟜𝕩⊸⊏⟜(∾⟜@)˝vars}
  Map ⇐ {@≡𝕩 ? 𝕨 ; 𝕩}¨⟜Var
}
csv‿util ← "/csv.bqn"‿"/util.bqn"•Import∘(∾˜)¨<⊑"."env.Map○⋈"BQN_LIB"
Colored ← {
 𝕊 n‿no‿name‿time‿0 : ⟨util.color.RGB 240‿20‿100⟩∾(¯1↓𝕩)∾⟨util.color.reset⟩ ;
 𝕊 n‿no‿name‿time‿1 : ⟨util.color.RGB 20‿180‿120⟩∾(¯1↓𝕩)∾⟨util.color.reset⟩
}
Replace ← {⊑𝕩∊𝕨 ? ' ' ; 𝕩}
Sat ← {𝕊 "" : ∞ ; 𝕊 "false" : 0 ; 𝕊 "true" : 1}
d ← 1↓>csv.SplitL•Flines util.AbsolutePath⊑•args
solved ← (∞⊸≠4⊸⊑)˘⊸/{⟨•BQN 3⊑𝕩,•BQN 1⊑𝕩,66(<⟜≠)◶⟨⊢,↑⟩2⊑𝕩,•ParseFloat 4⊑𝕩,Sat 6⊑𝕩⟩}˘d
•Out "┌─╵┘"""⊸Replace¨•Fmt Colored˘solved
